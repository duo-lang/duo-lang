module Codata.StreamStructural;


import Codata.Function;
import Data.Peano;
import Data.Bool;
import Data.List;
import Data.Tensor;

destructor Head(return CBV) : CBV;
destructor Tail(return CBV) : CBV;

-- | Map a function over a stream.
def rec prd streamMap : forall a b. (a -> b) -> (rec r. { Head(return a), Tail(return r) })  -> (rec r. { Head(return b), Tail(return r) }) :=
  \f s => cocase {
    Head(*) => f (s.Head(*)),
    Tail(*) => streamMap f (s.Tail(*))
};

-- | Filter elements of a stream.
def rec prd streamFilter : forall a. (a -> Bool) -> (rec r. { Head(return a), Tail(return r) }) -> (rec r. { Head(return a), Tail(return r) }) :=
  \p s => cocase {
    Head(*) =>  case p (s.Head(*)) of {
      True => s.Head(*),
      False => (streamFilter p (s.Tail(*))).Head(*)
    },
    Tail(*) => streamFilter p (s.Tail(*))
  };

-- | Interleave two streams.
def rec prd interleave : forall a. (rec r. { Head(return a), Tail(return r) }) -> (rec r. { Head(return a), Tail(return r) }) -> (rec r. { Head(return a), Tail(return r) }) :=
  \s t => cocase {
    Head(*) => s.Head(*),
    Tail(*) => interleave t (s.Tail(*))
  };

-- | Zip two streams to a stream of pairs.
def rec prd zip : forall a b. (rec r. { Head(return a), Tail(return r) }) -> (rec r. { Head(return b), Tail(return r) }) -> (rec r. { Head(return Tensor(a, b)), Tail(return r) }) :=
  \s t => cocase {
    Head(*) => MkTensor(s.Head(*), t.Head(*)),
    Tail(*) => zip (s.Tail(*)) (t.Tail(*))
  };

-- | Take n elements of a stream.
def rec prd take : forall a. Nat -> (rec r. { Head(return a), Tail(return r) }) -> List(a) :=
  \n s => case n of {
    Z => Nil,
    S(n) => Cons(s.Head(*), take n (s.Tail(*)))
  };

-- | Return a constant stream for the given element.
def rec prd constStream : forall a. a -> (rec r. { Head(return a), Tail(return r) }) :=
  \x => cocase {
    Head(*) => x,
    Tail(*) => constStream x
};

-- | Return a stream of repeated applications of f to x.
def rec prd iterate : forall a. (a -> a) -> a -> (rec r. { Head(return a), Tail(return r) }) :=
  \f x => cocase {
    Head(*) => x,
    Tail(*) => iterate f (f x)
  };

-- | Return a stream starting with natural number n.
def prd from : Nat -> (rec r. { Head(return Nat), Tail(return r) }) :=
  iterate (\n => S(n));
