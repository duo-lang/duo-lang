-- Booleans (Refinement)

import Function;

refinement data Bool : CBV {
    True,
    False
};

type RBool := < Bool | True, False >;

-- | Boolean negation.
def prd not : RBool -> RBool :=
  \b => case b of { True => False,
                    False => True
                  };

-- | Boolean if-then-else
def prd ifthenelse : forall a. RBool -> a -> a -> a :=
  \b e1 e2 => case b of { True => e1, False => e2 };

-- | Boolean conjunction
def prd and : RBool -> RBool -> RBool :=
  \b1 b2 => ifthenelse b1 b2 False;

-- | Boolean disjunction
def prd or : RBool -> RBool -> RBool :=
  \b1 b2 => ifthenelse b1 True b2;

-- | Boolean nand
def prd nand : RBool -> RBool -> RBool :=
  \b1 b2 => not (and b1 b2);

-- | Boolean nor
def prd nor : RBool -> RBool -> RBool :=
  \b1 b2 => not (or b1 b2);

-- | Boolean implication
def prd impl : RBool -> RBool -> RBool :=
  \b1 b2 => case b1 of {
      True => b2,
      False => True
  };
