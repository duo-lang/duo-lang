# Booleans (Nominal)

import Function;

data Bool : CBV {
    True,
    False
};

# Boolean negation.
prd not : Bool -> Bool :=
  \b => case b of { True => False,
                    False => True
                  };

# Boolean if-then-else
prd ifthenelse : forall a. Bool -> a -> a -> a :=
  \b e1 e2 => case b of { True => e1, False => e2 };

# Boolean conjunction
prd and : Bool -> Bool -> Bool :=
  \b1 b2 => ifthenelse b1 b2 False;

# Boolean disjunction
prd or : Bool -> Bool -> Bool :=
  \b1 b2 => ifthenelse b1 True b2;

# Boolean nand
prd nand : Bool -> Bool -> Bool :=
  \b1 b2 => not (and b1 b2);

# Boolean nor
prd nor : Bool -> Bool -> Bool :=
  \b1 b2 => not (or b1 b2);

# Boolean implication
prd impl : Bool -> Bool -> Bool :=
  \b1 b2 => case b1 of {
      True => b2,
      False => True
  };
